map "https://smartforms.csiro.au/ig/StructureMap/AUeReqFormExtract" = "AUeReqFormExtract"

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target

group bundleMap(source src : QuestionnaireResponse, target bundle : Bundle) {
    src.item -> bundle.type = 'transaction';
    src.item as rootItem then {
      rootItem.item as radiologyItem where linkId = 'radiology' -> bundle.entry as entry then{
        radiologyItem.linkId ->  entry.request as request then {
            radiologyItem.linkId -> request.method = 'POST';
            radiologyItem.linkId ->  entry.resource = create("ServiceRequest") as service then {
            radiologyItem.linkId -> service.requisition = create("Identifier") as requisition then {
                radiologyItem.linkId -> requisition.system = "https://smartforms.csiro.au/id/DiagnosticOrder";
                radiologyItem.linkId -> requisition.value = (%src.repeat(radiologyItem).where(linkId='radiology-order-number').answer.value);
                radiologyItem.linkId -> requisition.assigner as assigner then {
                    radiologyItem.linkId -> assigner.display = "AEHRC CSIRO";
                };	
                radiologyItem.linkId -> requisition.type as type then {
                    radiologyItem.linkId -> type.coding = create("Coding") as coding then {
                        radiologyItem.linkId -> coding.code = "PGN";
                        radiologyItem.linkId -> coding.system = "http://terminology.hl7.org/CodeSystem/v2-0203";
                        radiologyItem.linkId -> coding.display = "Placer Group Number";
                    };
                };
            };
            radiologyItem.linkId -> service.status = 'draft';
            radiologyItem.linkId -> service.intent = 'order';
            radiologyItem.linkId -> service.priority = (%radiologyItem.repeat(item).where(linkId='radiology-service-examination-priority').answer.value.code);
            radiologyItem.linkId -> service.category = create("CodeableConcept") as category then {
                radiologyItem.linkId -> category.text = "Imaging";
                radiologyItem.linkId -> category.coding = create("Coding") as coding then {
                    radiologyItem.linkId -> coding.code = "363679005";
                    radiologyItem.linkId -> coding.system = "http://snomed.info/sct";
                    radiologyItem.linkId -> coding.display = "Imaging";
                };
            };
            radiologyItem.linkId -> service.code = create("CodeableConcept") as code then {
                radiologyItem.linkId -> code.text = (%radiologyItem.repeat(item).where(linkId='radiology-service-examination-procedure').answer.value.display);
                radiologyItem.linkId -> code.coding = (%radiologyItem.repeat(item).where(linkId='radiology-service-examination-procedure').answer.value);
            };
            radiologyItem.linkId -> service.authoredOn = (now().toString().substring(0,10));
            radiologyItem.linkId -> service.bodySite = create("CodeableConcept") as code then {
                radiologyItem.linkId -> code.text = (%radiologyItem.repeat(item).where(linkId='radiology-service-examination-bodysite').answer.value.display);
                radiologyItem.linkId -> code.coding = (%radiologyItem.repeat(item).where(linkId='radiology-service-examination-bodysite').answer.value);
            };
            radiologyItem.linkId -> service.reasonCode = create("CodeableConcept") as code then {
                radiologyItem.linkId -> code.text = (%radiologyItem.repeat(item).where(linkId='radiology-service-clinicalindication').answer.value.display);
                radiologyItem.linkId -> code.coding = (%radiologyItem.repeat(item).where(linkId='radiology-service-clinicalindication').answer.value);
            };
            radiologyItem.linkId -> service.note = create("Annotation") as annotation then {
                radiologyItem.linkId -> annotation.text = (%radiologyItem.repeat(item).where(linkId='radiology-service-comment').answer.value);
            };
            radiologyItem.linkId -> service.note = create("Annotation") as annotation then {
                radiologyItem.linkId -> annotation.text = (%radiologyItem.repeat(item).where(linkId='radiology-service-clinicalnotes').answer.value);
            };
            radiologyItem.linkId -> service.requester as requester then {
                radiologyItem.linkId -> requester.reference = (%radiologyItem.repeat(item).where(linkId='radiology-order-requesterreference').answer.value);
                radiologyItem.linkId -> requester.display = (%radiologyItem.repeat(item).where(linkId='radiology-order-requesterdisplay').answer.value);
            };
            src.subject -> service.subject = (%src.subject);
            radiologyItem.linkId -> service.occurrence = (%src.repeat(item).where(linkId='radiology-service-examination-timing').answer.value);

        };
        };
      };

      rootItem.item as clinicalSummaryItem where linkId = 'clinicalsummary' then {
        clinicalSummaryItem.item as adverseReactionItem where linkId = 'clinicalsummary-adversereaction' -> bundle.entry as entry then {
        adverseReactionItem.linkId ->  entry.request as request then {
            adverseReactionItem.linkId -> request.method = 'POST';
            adverseReactionItem.linkId -> entry.resource = create("AllergyIntolerance") as allergy then {
                adverseReactionItem.item as substanceItem where linkId = 'clinicalsummary-adversereaction-substance' then {
                    substanceItem.answer -> allergy.code = create("CodeableConcept") as code then {
                        substanceItem.answer -> code.coding = (%substanceItem.answer.value);
                        substanceItem.answer -> code.text = (%substanceItem.answer.value.display);
                    };
                };
                adverseReactionItem.item as manifestationItem where linkId = 'clinicalsummary-adversereaction-manifestation' -> allergy.reaction as reaction then {
                    manifestationItem.answer as manifestationItemAnswer -> reaction.manifestation as manifestation then {
                        manifestationItemAnswer.value as manifestationItemAnswerValue -> manifestation.coding = manifestationItemAnswerValue then {
                            manifestationItemAnswerValue.display as manifestationItemAnswerValueDisplay -> manifestation.text = manifestationItemAnswerValueDisplay;
                        };
                    };
                };   
                adverseReactionItem.item as comment where linkId = 'clinicalsummary-adversereaction-comment' -> allergy.note = create("Annotation") as note then {
                    comment.answer -> note.text = (%comment.answer.value);
                };
                src.subject -> allergy.patient = (%src.subject);
            };
        };
      };
    };  
  };
}; 